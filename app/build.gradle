apply plugin: 'com.android.application'

ext {
    ciBuild = System.getenv("CI") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.circleci.demoapplication"
        minSdkVersion 25
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dexOptions {
      // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
      preDexLibraries = preDexEnabled && !ciBuild
    }
    buildTypes {
      release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // 10 minutes
        installOptions "-d", "-t"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}

apply plugin: "ru.gildor.gradle.firebase.testlab"

firebaseTestLab {
    gcloudPath = "/usr/lib/google-cloud-sdk/bin/"
    bucketName = "android_ci"

    copyArtifact {
        video = true
        instrumentation = true
    }

    matrices {

        nexus7 {
            // [REQUIRED] API level of devices
            androidApiLevels = [25]
            // [REQUIRED] Names of devices.
            deviceIds = ["flo"]
        }

    }
}
